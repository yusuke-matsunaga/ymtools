# Process this file with autoconf to produce a configure script
#
# $Id: configure.ac 2507 2009-10-17 16:24:02Z matsunaga $

# ===================================================================
# Initial block
# ===================================================================
AC_INIT([libym_utils], [1.0],
        [yusuke_matsunaga@ieee.org],
        [libym_utils])dnl

AC_REVISION($Rev: 2507 $)dnl

AC_PREREQ(2.57)dnl

AC_COPYRIGHT([
Copyright (C) 2005-2011 Yusuke Matsunaga
All rights reserved.
])dnl

AC_CONFIG_SRCDIR([ym_utils/Alloc.h])dnl

AC_CONFIG_AUX_DIR([config])dnl
AC_CONFIG_MACRO_DIR([config])dnl
AC_SUBST(ac_aux_dir)dnl

AM_INIT_AUTOMAKE()dnl


# ===================================================================
# Checks for programs
# ===================================================================
AC_ISC_POSIX
AC_PROG_MAKE_SET
AC_PROG_CC
AC_PROG_CXX
AC_PROG_LN_S
AC_PROG_INSTALL
AC_PROG_YACC
AM_PROG_LEX
AC_PROG_LIBTOOL

if test $enable_shared = no; then
  AC_DEFINE(BUILD_STATIC, 1, [Define for doing static link])
  AM_LDFLAGS="-static"
  AC_SUBST(AM_LDFLAGS)
fi


# ===================================================================
# Checks for libraries
# ===================================================================


# ===================================================================
# Checks for header files
# ===================================================================
AC_CHECK_HEADERS([pwd.h])
AC_CHECK_HEADERS([sys/param.h])

AC_CHECK_HEADERS([zlib.h], [ym_have_zlib=1])



# ===================================================================
# Checks for typedefs, structures, and compiler characteristics
# ===================================================================
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T


# ===================================================================
# Check for size of built-in types
# ===================================================================

AC_CHECK_SIZEOF(long)

_AC_COMPUTE_INT([__alignof__ (double)], ym_alignof_double)
AC_DEFINE_UNQUOTED(ALIGNOF_DOUBLE, $ym_alignof_double, [Alignment of double])


# ===================================================================
# Checks for library functions
# ===================================================================
#AC_FUNC_ALLOCA
AC_CHECK_FUNCS([random lrand48], break)
AC_CHECK_FUNCS([getrusage times], break)
AC_CHECK_FUNCS([getcwd gettimeofday strtod])


# ===================================================================
# Checks for other packages
# ===================================================================

# Check for Python
#YM_CHECK_PYTHON([ym_have_python=1], [ym_have_python=0])

AC_CACHE_SAVE


# ===================================================================
# Check for C++ language
# ===================================================================
AC_LANG_CPLUSPLUS


# ===================================================================
# Local definitions
# ===================================================================

YMTOOLS_SRCDIR="\$(top_srcdir)/../.."
YMTOOLS_BUILDDIR="\$(top_builddir)/../.."
AC_SUBST([YMTOOLS_SRCDIR])
AC_SUBST([YMTOOLS_BUILDDIR])

YM_EXTRA_SUBDIRS=""
YM_EXTRA_LIBADD=""

if test "$ym_have_zlib" = 1; then
   YM_EXTRA_SUBDIRS="$YM_EXTRA_SUBDIRS zstream"
   YM_EXTRA_LIBADD="$YM_EXTRA_LIBADD zstream/libym_zstream.la"
   AC_CONFIG_FILES([zstream/Makefile])
   AC_CONFIG_FILES([tests/zstream/Makefile])
fi

if test "$ym_have_python" = 1; then
   YM_EXTRA_SUBDIRS="$YM_EXTRA_SUBDIRS python"
   AC_CONFIG_FILES([python/Makefile])
   AC_CONFIG_FILES([tests/python/Makefile])
fi

AC_SUBST([YM_EXTRA_SUBDIRS])
AC_SUBST([YM_EXTRA_LIBADD])


# ===================================================================
# Declare files to be generated by configure script
# ===================================================================
AC_CONFIG_FILES([Makefile])
AC_CONFIG_FILES([ym_utils/Makefile])
AC_CONFIG_FILES([tests/Makefile])


# ===================================================================
# Generate configured files
# ===================================================================
AC_OUTPUT

# End-Of-File
